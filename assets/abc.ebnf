abc-tune -> abc-header abc-music

abc-header -> field-number comment* field-title other-fields* field-key
        
field-number -> "X:" DIGIT+ end-of-line
field-title -> "T:" text end-of-line
other-fields -> field-composer | field-default-length | field-meter 
	| field-tempo | field-voice | comment
field-composer -> "C:" text end-of-line
field-default-length -> "L:" note-length-strict end-of-line
field-meter -> "M:" meter end-of-line
field-tempo -> "Q:" tempo end-of-line
field-voice -> "V:" text end-of-line
field-key -> "K:" key end-of-line

key -> keynote [mode-minor]
keynote -> basenote [key-accidental]
key-accidental -> "#" | "b"
mode-minor -> "m"

meter -> "C" | "C|" | meter-fraction
meter-fraction -> DIGIT+ "/" DIGIT+ 

tempo -> DIGIT+ 

abc-music -> abc-line+
abc-line -> (element+ linefeed) | mid-tune-field | comment
element -> note-element | tuplet-element | barline | nth-repeat | space 

note-element -> (note | multi-note)

note -> note-or-rest [note-length]
note-or-rest -> pitch | rest
pitch -> [accidental] basenote [octave]
octave -> ("'"+) | (","+)
note-length -> [DIGIT+] ["/" [DIGIT+]]
note-length-strict -> DIGIT+ "/" DIGIT+

accidental -> "^" | "^^" | "_" | "__" | "="

basenote -> "C" | "D" | "E" | "F" | "G" | "A" | "B"
        | "c" | "d" | "e" | "f" | "g" | "a" | "b"

rest -> "z"

tuplet-element -> tuplet-spec note-element+
tuplet-spec -> "(" DIGIT 

multi-note -> "[" note+ "]"

barline -> "|" | "||" | "[|" | "|]" | ":|" | "|:"
nth-repeat -> "[1" | "[2"

mid-tune-field- -> field-voice

comment -> "%" text linefeed
end-of-line -> comment | linefeed

